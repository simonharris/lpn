
\chapter{Working With Files}\label{CHAPTER12}

\Thischapter{170}{

\noindent This chapter is concerned with various aspect of file handling and modularity.
We will learn three things:

\begin{enumerate}
\item{}How predicate definitions can be spread across different
        files.
\item{}How to write modular software systems.

\item{}How to write results to files and how to read input from
        files.
\end{enumerate}

}


\section{Splitting Programs over Files}\label{SEC.L12.SPLITTING.PROGRAMS}

By this stage you have written lots of programs that use the
predicates "append/3" and "member/2". What you probably did each time
you needed one of them was to go back to the definition and copy it
over to the file where you wanted to use it. And maybe, after having
done that a few times, you started thinking that it was quite annoying
having to copy the same predicate definitions over and over again ---
how pleasant it would be if you could define them somewhere once and
for all and then simply access them whenever you needed them. Well,
that sounds like a pretty sensible thing to ask for and, of course,
Prolog offers you ways of doing it.



\subsection*{Reading in programs}\label{SUBSEC.L12.READING.PROGRAMS}

In fact, you already know a way of telling Prolog to read in predicate
definitions that are stored in a file, namely the
\begin{LPNcodedisplay}
[FileName1]
\end{LPNcodedisplay}
command. You have been using queries of this form all along to tell
Prolog to \LPNterm{consult} files. But there are two more useful things
you should know about it.  First, you can consult many files at once
by saying
\begin{LPNcodedisplay}
[FileName1,FileName2,...,FileNameN]
\end{LPNcodedisplay}
instead. Second, and more importantly,
file consultation does \textit{not} have
to be performed interactively. If you put
\begin{LPNcodedisplay}
:- [FileName1,FileName2,...,FileNameN].
\end{LPNcodedisplay}
at the top of your program file (say "main.pl") you are telling Prolog
to first consult the listed files before going on to read in the rest
of your program.


This feature gives us a simple way of re-using definitions.  For
example, suppose that you keep all the predicate definitions for basic
list processing (such as "append/3", "member/2", "reverse/2", and so
on) in a file called "listPredicates.pl". If you want to use them,
simply put
\begin{LPNcodedisplay}
:- [listPredicates].
\end{LPNcodedisplay}
at the top of the file  containing the program that needs
them. Prolog will consult "listPredicates" when reading in that file,
and all the predicate definitions in "listPredicates" become
available.

There's one practical point you should be aware of. When Prolog loads
files, it doesn't normally check whether the files really need to be
consulted. If the predicate definitions provided by one of the files
are already in the database because that file was consulted
previously, Prolog will still consult it again, although it doesn't
need to. This can be annoying if you are consulting very large
files.

The built-in predicate
"ensure_loaded/1" behaves more intelligently  in this respect.  It
works as follows. On encountering the following
directive\index{PROLOG ensure_loaded/1@\texttt{ensure\_loaded/1}}
\begin{LPNcodedisplay}
:- ensure_loaded([listPredicates]).
\end{LPNcodedisplay}
Prolog checks whether the file "listPredicates.pl" has already been
loaded and only loads it again if it has changed since the last
loading.




\subsection*{Modules}\label{SUBSEC.L12.MODULES}

Now imagine that you are writing a program that manages a movie
database. You have designed a predicate "printActors" which displays
all actors starring in a particular film, and a predicate
"printMovies" which displays all movies directed by a particular
filmmaker. Both definitions are stored in different files, namely
"printActors.pl" and "printMovies.pl", and both use an auxiliary
predicate "displayList/1". Here's the first file:

\begin{LPNcodedisplay}
% This is the file: printActors.pl

printActors(Film):-
   setof(Actor,starring(Actor,Film),List),
   displayList(List).

displayList([]):- nl.
displayList([X|L]):-
   write(X), tab(1),
   displayList(L).
\end{LPNcodedisplay}
And here's the second:
\begin{LPNcodedisplay}
% This is the file: printMovies.pl

printMovies(Director):-
   setof(Film,directed(Director,Film),List),
   displayList(List).

displayList([]):- nl.
displayList([X|L]):-
   write(X), nl,
   displayList(L).
\end{LPNcodedisplay}

Note that "displayList/1" has different definitions in the two files:
the actors are printed in a row (using "tab/1")\index{PROLOG tab/1@\texttt{tab/1}}, and the films are printed in a column (using
"nl/0")\index{PROLOG nl/0@\texttt{nl/0}}.
Will this lead to conflicts in Prolog? Let's see. We'll
load both programs by placing the statements

\begin{LPNcodedisplay}
% This is the file: main.pl

:- [printActors].
:- [printMovies].
\end{LPNcodedisplay}
at the top of the main file. Consulting the main file will
evoke a message that  looks something like the
following:

\begin{LPNcodedisplay}
?- [main].
{consulting main.pl...}
{consulting printActors.pl...}
{printActors.pl consulted, 10 msec 296 bytes}
{consulting printMovies.pl...}
The procedure displayList/1 is being redefined.
    Old file: printActors.pl
    New file: printMovies.pl
Do you really want to redefine it? (y, n, p, or ?)
\end{LPNcodedisplay}

What has happened? Well, as both files "printActors.pl"
and "printMovies.pl"  define a predicate called "displayList/1", Prolog
needs to choose one of the two definitions (it can't have two different
definitions for one predicate in its knowledge base).

What to do? Well, perhaps in some of these situations you really do
want to redefine a predicate.  But here you don't --- you want two
different definitions because you want movies and actors to be
displayed differently.  One way of dealing with this is to give a
different name to one of the two predicates.  But let's face it, this
is clumsy. You want to think of each file as a conceptually
self-contained entity; you don't want to waste time and energy
thinking about how you named predicates in some other file.  And the
most natural way of achieving the desired conceptual independence is
to use Prolog's \LPNterm{module system}.

Modules essentially allow you to hide predicate definitions. You are
allowed to decide which predicates should be \LPNterm{public} (that is,
callable from parts of the program that are stored in other files)
and which predicates should be \LPNterm{private} (that is, callable
only from within the module itself).  Thus you will not be able to
call private predicates from outside the module in which they are
defined, but there will be no conflicts if two modules internally
define the same predicate. In our example,  "displayList/1" is a good
candidate for becoming a private predicate; it plays a simple
auxiliary role in  both "printActors/1" and  "printMovies/1,"
and the  details of the
role it plays for one predicate are not relevant to the other.

You can turn a file into a module by putting a \LPNterm{module declaration} at
the top. Module declarations are of the form   \index{PROLOG module/2@\texttt{module/2}}

\begin{LPNcodedisplay}
:- module(ModuleName,
          List_of_Predicates_to_be_Exported).
\end{LPNcodedisplay}
Such declarations  specify the name of the module and the list of public
predicates, that is, the list of predicates that you want to
\LPNterm{export}. These will be the only predicates that are
accessible from outside the module.

Let's modularise our movie database programs. We only need to
include the following line at the top of the first file:

\begin{LPNcodedisplay}
% This is the file: printActors.pl

:- module(printActors,[printActors/1]).

printActors(Film):-
   setof(Actor,starring(Actor,Film),List),
   displayList(List).

displayList([]):- nl.
displayList([X|L]):-
   write(X), tab(1),
   displayList(L).
\end{LPNcodedisplay}
Here we have introduced a module called "printActors",
with one public predicate "printActors/1". The predicate
"displayList/1" is only known in the scope of the module
"printActors", so its definition won't affect any other
modules.

Likewise we can turn the second file into a module:

\begin{LPNcodedisplay}
% This is the file: printMovies.pl

:- module(printMovies,[printMovies/1]).

printMovies(Director):-
   setof(Film,directed(Director,Film),List),
   displayList(List).

displayList([]):- nl.
displayList([X|L]):-
   write(X), nl,
   displayList(L).
\end{LPNcodedisplay}
Again, the  definition of the
"displayList/1" is only known in the scope of the module
"printMovies", so there won't be any
clash when loading both modules at the same time.

Modules can be loaded with the built-in predicates "use_module/1".
This will \LPNterm{import} all predicates that were defined as public
by the module. In other words, all public predicates will be\index{PROLOG use_module/1@\texttt{use\_module/1}}
accessible. To do this we need to change the main file as follows:

\begin{LPNcodedisplay}
% This is the file: main.pl

:- use_module(printActors).
:- use_module(printMovies).
\end{LPNcodedisplay}

If you don't want to use all public predicates of a module, but only
some of them, you can use the two-place version of "use_module", which
takes a list of predicates that you actually want to import as its
\index{PROLOG use_module/2@\texttt{use\_module/2}} second argument.
So, by putting

\begin{LPNcodedisplay}
% This is the file: main.pl

:- use_module(printActors,[printActors/1]).
:- use_module(printMovies,[printMovies/1]).
\end{LPNcodedisplay}
at the top of the main file, we have explicitly stated that we
can use "printActors/1" and "printMovies/1", and nothing else (in this
case, of course, the declaration is unnecessary as there are no other
public predicates that we could use).  Needless to say, you can only
import predicates that are actually exported by the relevant module.



\subsection*{Libraries}\label{SUBSEC.L12.LIBRARIES}

Many of the most common predicates are provided predefined, in one way
or another, by most Prolog implementations.  If you have been using
SWI Prolog, for example, you will probably have noticed that
predicates like "append/3" and "member/2" come as part of the
system. That's a speciality of SWI, however. Other Prolog
implementations, like SICStus for example, don't have them built-in,
but  provide them as part of a \LPNterm{library}.

Libraries are modules defining common predicates, and can be loaded
using the normal commands for importing modules. When specifying the
name of the library that you want to use, you have to tell Prolog that
this module is a library, so that Prolog knows where to look for it
(namely, in the place where Prolog keeps its libraries, not in the
directory where your other code is).
For example, putting the directive
\begin{LPNcodedisplay}
:- use_module(library(lists)).
\end{LPNcodedisplay}
at the top of your file tells Prolog to load a
library called "lists". In SICStus Prolog, this library contains a set of
commonly used list processing predicates.

Libraries can be very useful and they can save you a lot of
work. Moreover, the code in libraries has typically been written by
excellent programmers, and is likely to be highly efficient and
problem-free.  However the way that libraries are organised and the
inventory of predicates provided by libraries are by no means
standardised across different Prolog implementations. This means that
if you want your program to run with different Prolog implementations,
it is probably easier and faster to define your own library modules
(using the techniques that we saw in the last section) rather
than to try to work around the incompatibilities between the library
systems of different Prolog implementations.



\section{Writing to Files}\label{SEC.L12.FILE.WRITING}

Many applications require that output be written to a file rather than
to the screen. In this section we will explain how to do this in
Prolog.

In order to write to a file we have to create one (or open an existing
one) and associate a \LPNterm{stream} with it. You can think of streams
as connections to files. In Prolog, streams are blessed with names in
a rather user-unfriendly format, such as
"'\$stream'(183368)". Luckily, you never have to bother about the
exact names of streams --- although Prolog assigns these names
internally, you can use Prolog's unification to match the name to a
variable and make use of the variable rather than the name of the
stream itself.

Say you want to print the string 'Hogwarts' to the file "hogwarts.txt".
This is done as follows:
\index{PROLOG open/3@\texttt{open/3}}
\index{PROLOG write/2@\texttt{write/2}}
\index{PROLOG close/1@\texttt{close/1}}
\index{PROLOG nl/1@\texttt{nl/1}}

\begin{LPNcodedisplay}
   ...
   open('hogwarts.txt',write,Stream),
   write(Stream,'Hogwarts'), nl(Stream),
   close(Stream),
   ...
\end{LPNcodedisplay}

What's happening here? Well, first the built-in predicate "open/3" is
used to create the file "hogwarts.txt". The second argument of
"open/3" indicates that we want to \LPNterm{open} a new file
(overwriting any existing file with the same name). The third argument
of "open/3" returns the name of the stream. Secondly, we
\LPNterm{write} 'Hogwarts' on the stream and issue a newline command as
well. After this we are ready, and \LPNterm{close} the stream, using
the built-in "close/1".

And that's more or less all there is to it.  As promised, we were not
interested in the name of the stream --- we used the variable "Stream"
to pass it around. Also note that the "write/2" predicate we used here
is basically a more general form of the "write/1" predicates we used
in Chapter~\ref{CHAPTER9} for writing to the screen.

What if you don't want to overwrite an existing file but
\LPNterm{append} to an existing one? This is done by choosing a
different mode when opening the file: instead of "write", use "append"
as value for the second argument of "open/3".  If a file of the
given name doesn't exist, it will be created.






\section{Reading from Files}\label{SEC.L12.FILE.READING}

In this section we show how to read from files.  Reading information
from files is straightforward in Prolog --- or at least, it is if this
information is given in the form of Prolog terms followed
by full stops. Consider the file "houses.txt":

\begin{LPNcodedisplay}
gryffindor.
hufflepuff.
ravenclaw.
slytherin.
\end{LPNcodedisplay}
Here is a Prolog program that opens this file, reads\index{PROLOG read/2@\texttt{read/2}}\index{PROLOG open/3@\texttt{open/3}}
the information from it, and displays it on the screen:

\begin{LPNcodedisplay}
main:-
   open('houses.txt',read,Str),
   read(Str,House1),
   read(Str,House2),
   read(Str,House3),
   read(Str,House4),
   close(Str),
   write([House1,House2,House3,House4]), nl.
\end{LPNcodedisplay}
This opens a file in \LPNterm{reading mode}, then reads
four Prolog terms using the built-in predicate "read/2",
closes the stream, and prints the information as a list.

All very straightforward. Nonetheless, the "read/2" predicate needs to
be handled with care. First of all, it only is able to handle Prolog
terms (we'll say more about this problem shortly). And secondly, it
will cause a run-time error if we use it to read from a stream when
there is nothing to read. Is there an elegant way to overcome this
second problem?

There is. The built-in predicate "at_end_of_stream/1" checks whether
the \LPNterm{end of a stream} has been reached, and can be
used as a safety-net.  For a stream "X", "at_end_of_stream(X)"
will\index{PROLOG at_end_of_stream/1@\texttt{at\_end\_of\_stream/1}}
evaluate to true  when the end of the stream "X" is reached (in other
words, when all terms in the corresponding file have been read).

The following code is a modified version of our earlier reading-in
program, which shows how "at_end_of_stream/1" can be incorporated:

\begin{LPNcodedisplay}
main:-
   open('houses.txt',read,Str),
   read_houses(Str,Houses),
   close(Str),
   write(Houses), nl.

read_houses(Stream,[]):-
   at_end_of_stream(Stream).

read_houses(Stream,[X|L]):-
   \+ at_end_of_stream(Stream),
   read(Stream,X),
   read_houses(Stream,L).
\end{LPNcodedisplay}

Now for the nastier problem.  Recall that "read/2" only reads in
Prolog terms. If you want to read in arbitrary input, things become
rather unpleasant, as Prolog forces you to read information on the
level of characters.  The predicate that you need in this case is
"get_code/2" which\index{PROLOG get\_code/2@\texttt{get\_code/2}} reads the next
available character from a stream.  Characters are represented in
Prolog by their integer codes. For example, "get_code/2" will return "97"
if the next character on the stream is an "a".

Usually we are not interested in these integer codes, but in the
characters --- or rather, in the atoms that are made up of lists of
these characters. How do we get our hands on these (lists of)
characters? One way is to use the built-in predicate "atom_codes/2"
that\index{PROLOG atom\_codes/2@\texttt{atom\_codes/2}} we introduced in
Chapter~\ref{CHAPTER9} to convert a list of integers into the
corresponding atom. We'll use this technique in the following example,
a predicate that reads in a word from a
stream.

\begin{LPNcodedisplay}
readWord(InStream,W):-
   get_code(InStream,Char),
   checkCharAndReadRest(Char,Chars,InStream),
   atom_codes(W,Chars).


checkCharAndReadRest(10,[],_):- !.

checkCharAndReadRest(32,[],_):- !.

checkCharAndReadRest(-1,[],_):- !.

checkCharAndReadRest(end_of_file,[],_):- !.

checkCharAndReadRest(Char,[Char|Chars],InStream):-
   get_code(InStream,NextChar),
   checkCharAndReadRest(NextChar,Chars,InStream).
\end{LPNcodedisplay}

How does this work?  It reads in a character and then checks whether
this character is a blank (integer code 32), a new line (10) or the
end of the stream ($-1$). In any of these cases a complete word has been
read, otherwise the next character is read.


\section{Exercises}\label{SEC.L12.EXERCISES}

\begin{LPNexercise}{L12.EX1}
Write code that creates  "hogwart.houses", a file that  that looks
like this:
\begin{LPNcodedisplay}
       gryffindor
hufflepuff     ravenclaw
       slytherin
\end{LPNcodedisplay}
You can use the built-in predicates "open/3", "close/1", "tab/2",
"nl/1", and "write/2".
\end{LPNexercise}


\begin{LPNexercise}{L12.EX2}
Write a Prolog program that reads in a plain text file word by word,
and asserts all read words and their frequency into the Prolog database. You may use the
predicate "readWord/2" to read in words. Use a dynamic predicate
"word/2" to store the words, where the first argument is a word, and
the second argument is the frequency of that word.
\end{LPNexercise}



\section{Practical Session}\label{SEC.L12.PRAXIS}

In this practical session, we want to combine what we have learned
about file handling with some topics we met in earlier chapters. The
goal is to write a program for running a DCG grammar on a testsuite,
so that the performance of the grammar can be checked.

What is a testsuite? It is a file that contains lots of possible
inputs (and expected outputs) for some program. In this case, a
testsuite will be a file that has lists representing grammatical and
ungrammatical sentences, such as
"[the,woman,shoots,the,cow,under,the,shower]" or
"[him,shoots,woman]". The test program should take this file, run the
grammar on each of the sentences, and store the results in another
file. We can then look at the output file to check whether the grammar
answered everywhere the way it should have. When developing grammars,
testsuites like this are extremely useful for making sure that any
modifications we make to the grammar don't have unwanted effects.



\subsection*{Step 1}\label{L12.PRAXIS.STEP1}

Take the DCG that you built in the practical session of
Chapter~\ref{CHAPTER8} and turn it into a module, exporting the
predicate "s/3", that is, the predicate that lets you parse sentences and
returns the parse tree as its first argument.




\subsection*{Step 2}\label{L12.PRAXIS.STEP2}

In the practical session of Chapter~\ref{CHAPTER9}, you
had to write a program for pretty printing parse trees onto the
screen. Turn that into a module as well.



\subsection*{Step 3}\label{L12.PRAXIS.STEP3}

Now modify the program so that it prints the tree not to the
screen but to a given stream. That means that the predicate
"pptree" should now be a two-place predicate taking the Prolog
representation of a parse tree and a stream as arguments.




\subsection*{Step 4}\label{L12.PRAXIS.STEP4}

Import both modules into a file and define a two-place predicate
"test" which takes a list representing a sentence (such as
"[a,woman,shoots]"), parses it,  and writes the result to the file
specified by the second argument of "test".
Check that everything is working as it should.



\subsection*{Step 5}\label{L12.PRAXIS.STEP5}

Finally, modify "test/2", so that it takes a filename instead of a
sentence as its first argument, reads in the sentences given in the
file one by one, parses them, and writes the sentence as well as the
parsing result into the output file. For example, if your input file
looked like this:
\begin{LPNcodedisplay}
[the,cow,under,the,table,shoots].

[a,dead,woman,likes,he].
\end{LPNcodedisplay}

the output file should look something like this:
\begin{LPNcodedisplay}
[the, cow, under, the, table, shoots]

   s(
      np(
         det(the)
         nbar(
            n(cow))
         pp(
            prep(under)
            np(
               det(the)
               nbar(
                  n(table)))))
      vp(
         v(shoots)))


[a, dead, woman, likes, he]

no
\end{LPNcodedisplay}



\subsection*{Step 6}\label{L12.PRAXIS.STEP6}

Now (if you are in for some real Prolog hacking) try to write a module
that reads in sentences terminated by a full stop or a line break from
a file, so that you can give your testsuite as
\begin{LPNcodedisplay}
the cow under the table shoots .

a dead woman likes he .
\end{LPNcodedisplay}

instead of
\begin{LPNcodedisplay}
[the,cow,under,the,table,shoots].

[a,dead,woman,likes,he].
\end{LPNcodedisplay}


\subsection*{Step 7}\label{L12.PRAXIS.STEP7}

Make the testsuite environment more sophisticated, by adding
information to the input file about the expected output (in this case,
whether the sentences has a parse or not). Then modify the program so
that it checks whether the expected output matches the obtained
output.
