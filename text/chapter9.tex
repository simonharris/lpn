
\chapter{A Closer Look at Terms}\label{CHAPTER9}

\Thischapter{115}{

\noindent
This chapter has three main goals:

\begin{enumerate}

\item{}To introduce the \texttt{==} predicate.

\item{}To take a closer look at term structure.

\item{}To introduce operators.

\end{enumerate}

}

\section{Comparing Terms}\label{SEC.L9.COMPARING.TERMS}



Prolog contains an important predicate for comparing terms, namely the
\LPNterm{identity predicate} "==/2". As its name suggests, this tests
whether two terms are identical.  However "==/2" does \LPNemph{not}
instantiate variables, thus it is not the same as the unification
predicate "=/2".\index{PROLOG ==/2@\texttt{==/2}} Let's look at some
examples.
\begin{LPNcodedisplay}
?- a == a.
yes

?- a == b.
no

?- a == 'a'.
yes
\end{LPNcodedisplay}
The reason Prolog gives these answers should be clear, though pay
attention to the last one.  It tells us that, as far as Prolog is
concerned, "a" and "'a'" are the same object.

Now let's look at examples involving variables, and explicitly compare
"==" with the unification predicate "=".

\begin{LPNcodedisplay}
?- X==Y.
no

?- X=Y.
X = _2808
Y = _2808
yes
\end{LPNcodedisplay}
In these queries, "X" and "Y" are \LPNemph{uninstantiated}
variables; we haven't given them any value.  Thus the first answer is
correct: "X" and "Y" are \LPNemph{not} identical objects, so the
"==" test fails.  On the other hand, the use of "=" succeeds,
for "X" and "Y" can be unified.

Let's now look at queries involving \LPNemph{instantiated} variables:
\begin{LPNcodedisplay}
?- a=X, a==X.

X = a
yes
\end{LPNcodedisplay}
The first conjunct, "a=X", binds "X" to "a".  Thus when
"a==X" is evaluated, the left hand side and right hand sides are
exactly the same Prolog object, and "a==X" succeeds.

A similar thing happens in the following query:
\begin{LPNcodedisplay}
?- X=Y, X==Y.

X = _4500
Y = _4500
yes
\end{LPNcodedisplay}
The conjunct "X=Y" first unifies the variables "X" and "Y".  Thus when
the second conjunct "X==Y" is evaluated, the two variables are exactly
the same Prolog object, and the second conjunct succeeds as well.

It should now be clear that "=" and "==" are different, nonetheless
there is an important relation between them: "==" can be viewed as a
stronger test for equality between terms than "=".  That is, if
"term1" and "term" are Prolog terms, and the query "term1 == term2"
succeeds, then the query "term1 = term2" will succeed too.

Another predicate worth knowing about is "\==".  This predicate is
defined so that it succeeds in precisely those cases where "==" fails.
That is, it succeeds whenever two terms are \LPNemph{not}\index{PROLOG
\==/2@\verb-\==/2-} identical, and fails otherwise.  For example:
\begin{LPNcodedisplay}
?- a \== a.
no

?- a \== b.
yes

?- a \== 'a'.
no
\end{LPNcodedisplay}
These answers should be understandable: they are simply the opposite of the
answers we got above when we used "==". Now consider:
\begin{LPNcodedisplay}
?- X \== a.

X = _3719
yes
\end{LPNcodedisplay}
Why this response?  Well, we know from above that the query "X==a"
\LPNemph{fails} (recall the way "==" treats uninstantiated variables).
Thus the query "X\==a" should \LPNemph{succeed}, and it does.

Similarly:
\begin{LPNcodedisplay}
?- X \== Y.

X = _798
Y = _799
yes
\end{LPNcodedisplay}
Again, we know from above that the query "X==Y" fails, thus the query
"X\==Y" succeeds.

\section{Terms with a Special Notation}\label{SEC.L9.SPECIAL.NOTATION}



Sometimes terms look different to us, but Prolog regards them as
identical.  For example, when we compare "a" and "'a'", we see two
distinct strings of symbols, but Prolog treats them as the same.  And
in fact there are many other cases where Prolog regards two strings as
being exactly the same term.  Why?  Because it makes programming more
pleasant.  Sometimes the notation Prolog likes isn't as user-friendly
as the notation we would choose.  So it is nice to be able to write
programs in the notation we find natural, and to let Prolog run them
in the notation it prefers.

\subsection*{Arithmetic terms}\label{SUBSEC.L9.ARITHMETIC.TERMS}



The arithmetic predicates introduced earlier are a good example of
this.  As was mentioned in Chapter~\ref{CHAPTER5}, "+", "-", "*", and
"/" are \LPNemph{functors}, and arithmetic expressions such as " 2+3"
are \LPNemph{terms}.  And this is not an analogy.  Apart from the fact
that it can evaluate them with the help of the "is/2" predicate,
Prolog views strings of symbols such as "2+3" as being identical with
ordinary complex terms. The following queries make this clear:
\begin{LPNcodedisplay}
?- 2+3 == +(2,3).
yes

?- +(2,3) == 2+3.
yes

?- 2-3 == -(2,3).
yes

?- *(2,3) == 2*3.
yes

?- 2*(7+2) == *(2,+(7,2)).
yes
\end{LPNcodedisplay}
In short, the familiar arithmetic notation is there for \LPNemph{our}
convenience.  Prolog doesn't regard it as different from the usual
term notation.

Similar remarks to the arithmetic comparison predicates "<", "=<", "=:=", "=\=", ">" and ">=":
\begin{LPNcodedisplay}
?- (2 < 3) == <(2,3).
yes

?- (2 =< 3) == =<(2,3).
yes

?- (2 =:= 3) == =:=(2,3).
yes

?- (2 =\= 3) == =\=(2,3).
yes

?- (2 > 3) == >(2,3).
yes

?- (2 >= 3) == >=(2,3).
yes
\end{LPNcodedisplay}

These example show why it's nice to have the user-friendly notation
(would you want to have to work with expressions like
"=:=(2,3)"?). Note, by the way, that we enclosed the left hand
arguments in brackets. For example, we didn't ask
\begin{LPNcodedisplay}
?- 2 =:= 3 == =:=(2,3).
\end{LPNcodedisplay}
we asked
\begin{LPNcodedisplay}
?- (2 =:= 3) == =:=(2,3).
\end{LPNcodedisplay}
Why?  Well, Prolog finds the query "2 =:= 3 == =:=(2,3)" confusing,
and let's face it,  can you blame it?  It's not sure whether to bracket
this expression as "(2 =:= 3) == =:=(2,3)" (which is what we want), or
as "2 =:= (3 == =:=(2,3))".  So we need to state the grouping
explicitly.

One final remark.  We have now introduced three rather similar looking
symbols, namely "=", "==", and "=:=" (and indeed, there are
also "\=", "\==", and "=\=").  Here's a summary:

\begin{center}\begin{tabular}{ll}
"="&The unification predicate.\\
\strut&Succeeds if it can
        unify its arguments, fails otherwise.\\
"\="&The negation of the  unification predicate.\\
\strut&Succeeds if "=" fails, and vice-versa.\\
"=="&The identity predicate.\\
\strut&Succeeds if its arguments
        are identical, fails otherwise.\\
"\=="&The negation of the  identity predicate.\\
\strut&Succeeds if "==" fails, and vice-versa.\\
"=:="&The arithmetic equality predicate.\\
\strut&Succeeds if its arguments evaluate to the same integer.\\
"=\="&The arithmetic inequality predicate.\\
\strut&Succeeds if its
arguments evaluate to different integers.
\end{tabular}\end{center}



\subsection*{Lists as terms}\label{SUBSEC.L9.LIST.TERMS}



Lists are another good example of Prolog working with one internal
representation, while giving us another, more user-friendly, notation
to work with.  Let's start with a quick look at the user-friendly list
notation it provides (that is, the square brackets "[" and "]").  In
fact, because Prolog also offers the "|" constructor, there are
many ways of writing the same list, even at the user-friendly level:

\begin{LPNcodedisplay}
?- [a,b,c,d] == [a|[b,c,d]].
yes

?- [a,b,c,d] == [a,b|[c,d]].
yes

?- [a,b,c,d] == [a,b,c|[d]].
yes

?- [a,b,c,d] == [a,b,c,d|[]].
yes
\end{LPNcodedisplay}



But how does Prolog view lists internally?  In fact, it sees lists as
terms which are built out of two special terms, namely "[]", which
represents the empty list, and ``"."'' (the full-stop), a functor
of\index{PROLOG ./2@\texttt{./2}} arity 2 which is used to build
non-empty lists. The terms "[]" and "." are called \LPNterm{list
constructors}.

This is how these constructors are used to build lists.  Needless to
say, the definition is recursive:

\begin{itemize}
\item{}The empty list is the term $[ \ ]$. It
        has length 0.
\item{}A non-empty list is any term of the form $.(\textit{term},\textit{list})$,
        where \textit{term} is any Prolog term, and \textit{list} is any
        list.  If \textit{list} has length $n$, then $.(\textit{term},\textit{list})$ has
        length $n+1$.
\end{itemize}

Let's make sure we fully understand this definition by working our way
through a few examples.


\begin{LPNcodedisplay}
?- .(a,[]) == [a].
yes

?- .(f(d,e),[]) == [f(d,e)].
yes

?- .(a,.(b,[])) == [a,b].
yes

?- .(a,.(b,.(f(d,e),[]))) == [a,b,f(d,e)].
yes

?- .(.(a,[]),[]) == [[a]].
yes

?- .(.(.(a,[]),[]),[]) == [[[a]]].
yes

?- .(.(a,.(b,[])),[]) == [[a,b]].
yes

?- .(.(a,.(b,[])),.(c,[])) == [[a,b],c].
yes

?- .(.(a,[]),.(b,.(c,[]))) == [[a],b,c].
yes

?- .(.(a,[]),.(.(b,.(c,[])),[])) == [[a],[b,c]].
yes
\end{LPNcodedisplay}


Prolog's internal notation for lists is not as user-friendly as the
use of the square bracket notation.  But it's not as bad as it seems
at first sight. In fact, it works similarly to the "|" notation. It
represents a list in two parts: its first element (the head), and a
list representing the rest of the list (the tail).  The trick is to
read these terms as \LPNemph{trees}. The internal nodes of this tree
are labeled with "." and all have two daughter nodes.  The subtree
under the left daughter represents the first element of the list and
the subtree under the right daughter represents the rest of the list.
For example, the tree representation of
".(a,.(.(b,.(c,[])),.(d,[])))", that is, "[a, [b,c], d]", looks like
this:

\begin{center}
\includegraphics[scale=.7]{listtree.eps}
\end{center}


One final remark. Prolog is very polite. Not only are you free to talk
to it in the user-friendly notation, it will reply in the same way:
\begin{LPNcodedisplay}
?- .(f(d,e),[]) = Y.

Y = [f(d,e)]
yes


?- .(a,.(b,[])) = X, Z= .(.(c,[]),[]), W = [1,2,X].

X = [a,b]
Z = [[c]]
W = [1,2,[a,b]]
yes
\end{LPNcodedisplay}


\section{Examining Terms}\label{SEC.L9.EXAMINING}



In this section, we will learn about some built-in predicates that let
us examine terms more closely. First, we will look at predicates that
test whether their arguments are terms of a certain type (for example,
whether they are atoms or numbers). Then we will introduce predicates
that tell us something about the internal structure of complex terms.


\subsection*{Types of Terms}\label{SUBSEC.L9.TERMTYPES}

Remember what we said about Prolog terms in Chapter~\ref{CHAPTER1}:
there are four different kinds, namely variables, atoms, numbers and
complex terms.  Furthermore, atoms and numbers are grouped together
under the name constants, and constants and variables constitute the
simple terms. The following tree diagram summarises this:

\begin{center}
  \pstree[levelsep=10mm,nodesep=2mm]{\TR{terms}}
    { \pstree[levelsep=10mm,nodesep=2mm]{\TR{simple terms}}
         { \TR{variables}
           \pstree[levelsep=10mm,nodesep=2mm]{\TR{constants}}
               { \TR{atoms}
                 \TR{numbers} }}
      \TR{complex terms} }

%\includegraphics{termtypes.eps}
\end{center}

Sometimes it is useful to be able to determine what type a given term
is. You might, for example, want to write a predicate that has to deal
with different kinds of terms, but has to treat them in different
ways.  Prolog provides several built-in predicates that test whether a
given term is of a certain type:

\begin{center}\begin{tabular}{ll}
"atom/1"&Is the argument an atom?\\ \index{PROLOG atom/1@\texttt{atom/1}}
"integer/1"&Is  the argument  an integer?\\ \index{PROLOG integer/1@\texttt{integer/1}}
"float/1"&Is the argument a floating point number?\\ \index{PROLOG float/1@\texttt{float/1}}
"number/1"&Is  the argument an integer or a floating point number?\\ \index{PROLOG number/1@\texttt{number/1}}
"atomic/1"&Is the  argument a constant?\\ \index{PROLOG atomic/1@\texttt{atomic/1}}
"var/1"&Is the argument an uninstantiated variable?\\ \index{PROLOG var/1@\texttt{var/1}}
"nonvar/1"&\parbox[t]{0.75\textwidth}{Is  the argument an instantiated variable or another term that is not an \LPNemph{un}instantiated variable?} \index{PROLOG nonvar/1@\texttt{nonvar/1}}
\end{tabular}\end{center}


Let's see how they behave.

\begin{LPNcodedisplay}
?- atom(a).
yes

?- atom(7).
no

?- atom(loves(vincent,mia)).
no
\end{LPNcodedisplay}
These three examples behave exactly as we would expect.  But what
happens, when we call "atom/1" with a variable as argument?
\begin{LPNcodedisplay}
?- atom(X).
no
\end{LPNcodedisplay}
This makes sense, since an uninstantiated variable is not an atom.
However if we instantiate "X" with an atom first and then ask
"atom(X)", Prolog answers yes.
\begin{LPNcodedisplay}
?- X = a, atom(X).
X = a
yes
\end{LPNcodedisplay}
But it is important that the instantiation is done \LPNemph{before} the
test:
\begin{LPNcodedisplay}
?- atom(X), X = a.
no
\end{LPNcodedisplay}
The predicates "integer/1" and "float/1" behave analogously.  Try some
examples.

The predicates "number/1" and "atomic/1" behave disjunctively.  First,
"number/1" tests whether a given term is either an integer or a float:
that is, it will evaluate to true whenever either "integer/1" or
"float/1" evaluate to true and it fails when both of them fail.  As
for "atomic/1", this tests whether a given term is a constant, that
is, whether it is either an atom or a number. So "atomic/1" will
evaluate to true whenever either "atom/1" or "number/1" evaluate to
true and it fails when both fail.

\begin{LPNcodedisplay}
?- atomic(mia).
yes

?- atomic(8).
yes

?- atomic(3.25).
yes

?- atomic(loves(vincent,mia)).
no

?- atomic(X)
no
\end{LPNcodedisplay}

What about variables?  First there is the "var/1" predicate. This
tests whether the argument is an \LPNemph{uninstantiated} variable:
\begin{LPNcodedisplay}
?- var(X)
yes

?- var(mia).
no

?- var(8).
no

?- var(3.25).
no

?- var(loves(vincent,mia)).
no
\end{LPNcodedisplay}

Then there is the "nonvar/1" predicate. This succeeds precisely when
"var/1" fails; that is, it tests whether its argument is \LPNemph{not}
an uninstantiated variable:
\begin{LPNcodedisplay}
?- nonvar(X)
no

?- nonvar(mia).
yes

?- nonvar(8).
yes

?- nonvar(3.25).
yes

?- nonvar(loves(vincent,mia)).
yes
\end{LPNcodedisplay}

Note that a complex term which contains uninstantiated variables is
not itself an uninstantiated variable (it is a  complex
term). Therefore we have:

\begin{LPNcodedisplay}
?- var(loves(_,mia)).
no

?- nonvar(loves(_,mia)).
yes
\end{LPNcodedisplay}

And  when the variable "X" gets instantiated "var(X)"
and "nonvar(X)" behave differently depending on whether they are
called before or after the instantiation:

\begin{LPNcodedisplay}
?- X = a, var(X).
no

?- X = a, nonvar(X).
X = a
yes

?- var(X), X = a.
X = a
yes

?- nonvar(X), X = a.
no
\end{LPNcodedisplay}




\subsection*{The Structure of Terms}\label{SUBSEC.L9.TERMSTRUCTURE}

Given a complex term of unknown structure (perhaps a complex term
returned as the output of some predicate), what kind of information
might we want to extract from it? The obvious response is: its
functor, its arity, and what its arguments look like. Prolog
provides built-in predicates that provide this
information. Information about the functor\index{PROLOG
functor/3@\texttt{functor/3}} and arity is supplied by the predicate
"functor/3".  Given a complex term, "functor/3" will tell us what its
functor and arity are:

\begin{LPNcodedisplay}
?- functor(f(a,b),F,A).
A = 2
F = f
yes

?- functor([a,b,c],X,Y).
X = '.'
Y = 2
yes
\end{LPNcodedisplay}
Note that when asked about a list, Prolog returns the functor ., which
is the functor it uses in its internal representation of lists.

What happens when we use "functor/3" with constants? Let's try:
\begin{LPNcodedisplay}
?- functor(mia,F,A).
A = 0
F = mia
yes

?- functor(8,F,A).
A = 0
F = 8
yes

?- functor(3.25,F,A).
A = 0
F = 3.25
yes
\end{LPNcodedisplay}

So we can use the predicate "functor/3" to find out the functor and the
arity of a term, and this usage also works for the special case of 0
arity terms (constants).

We can also use
"functor/3" to \LPNemph{construct} terms. How?
By specifying the second and third argument and leaving the first
undetermined.  The query
\begin{LPNcodedisplay}
?- functor(T,f,7).
\end{LPNcodedisplay}
for example, returns the following answer:
\begin{LPNcodedisplay}
T = f(_G286, _G287, _G288, _G289, _G290, _G291, _G292)
yes
\end{LPNcodedisplay}
Note that either the first argument or the second and third argument
have to be instantiated. For example, Prolog would answer with an
error message to the query "functor(T,f,N)". And if you think about
what the query means, Prolog is  reacting in a sensible
way. The query is asking Prolog to construct a complex term without
telling it how many arguments to provide, which is not a very sensible
request.

Now that we know about "functor/3", let's put it to work.  In the
previous section, we discussed the built-in predicates that tested
whether their argument was an atom, a number, a constant, or a
variable. But there was no predicate that tested whether its argument
was a complex term.  To make the list complete, let's define such a
predicate. It is easy to do so using "functor/3".  All we have to do
is to check that there is a suitable functor, and that the input has
arguments (that is, that its arity is greater than zero).  Here is the
definition:

\begin{LPNcodedisplay}
complexterm(X):-
   nonvar(X),
   functor(X,_,A),
   A > 0.
\end{LPNcodedisplay}

So much for functors --- what about arguments?  In addition to
the\index{PROLOG arg/3@\texttt{arg/3}} predicate "functor/3", Prolog
supplies us with the predicate "arg/3" which tells us about the
arguments of complex terms. It takes a number \LPNemph{N} and a
complex term \LPNemph{T} and returns the \LPNemph{Nth} argument of
\LPNemph{T} in its third argument.  It can be used to access the value
of an argument
\begin{LPNcodedisplay}
?- arg(2,loves(vincent,mia),X).
X = mia
yes
\end{LPNcodedisplay}
or to instantiate an argument
\begin{LPNcodedisplay}
?- arg(2,loves(vincent,X),mia).
X = mia
yes
\end{LPNcodedisplay}
Trying to access an argument which doesn't exist, of course, fails:
\begin{LPNcodedisplay}
?- arg(2,happy(yolanda),X).
no
\end{LPNcodedisplay}

The predicates "functor/3" and "arg/3" allow us to access all the
basic information we need to know about complex terms. However Prolog
also supplies a third built-in predicate for analysing term structure,
namely "'=..'/2". This takes a complex term and returns a list that
has the functor as its head, and then all the arguments, in order, as
the elements of the tail.  So to the query
\begin{LPNcodedisplay}
?- '=..'(loves(vincent,mia),X)
\end{LPNcodedisplay}
Prolog will respond
\begin{LPNcodedisplay}
"X = [loves,vincent,mia]
\end{LPNcodedisplay}
This predicate (which is called \LPNterm{univ}) can also be used as an
infix operator. Here are some examples showing various ways of
using\index{PROLOG =..@\texttt{=../2}} this (very useful) tool:
\begin{LPNcodedisplay}
?- cause(vincent,dead(zed)) =.. X.
X = [cause, vincent, dead(zed)]
yes

?- X =.. [a,b(c),d].
X = a(b(c), d)
yes

?- footmassage(Y,mia) =.. X.
Y = _G303
X = [footmassage, _G303, mia]
yes
\end{LPNcodedisplay}

Univ really comes into its own when something has to be done to all
arguments of a complex term. Since it returns the arguments as a list,
normal list processing strategies can be used to traverse the
arguments.


\subsection*{Strings}\label{SUBSEC.L9.STRINGS}

Strings are represented in Prolog by a list of character (ASCII)
codes. However, it would be a right kerfuffle to use list notation for
simple string manipulation, so Prolog also offers a user-friendly
notation for strings: double quotes. Try the following query:

\begin{LPNcodedisplay}
?- S = "Vicky".
S = [86, 105, 99, 107, 121]
yes
\end{LPNcodedisplay}
Here the variable \texttt{S} unifies with the string \texttt{"Vicky"},
which is a list containing of five numbers, each of them corresponding
to the character codes of the single characters the strings is
composed of.  (For instance, 86 is the character code for the
character V, 105 is the code for the character i, and so on.)

In other words, strings in Prolog are actually lists of
numbers. Several standard predicates are supported by most Prolog
dialects to work with strings. A particularly useful one is
\texttt{atom\_codes/2}. This predicate converts an atom into a string. The
following examples illustrate what \texttt{atom\_codes/2}\index{PROLOG
atom\_codes/2@\texttt{atom\_codes/2}} can do for you:

\begin{LPNcodedisplay}
?- atom_codes(vicky,X).
X = [118, 105, 99, 107, 121]
yes

?- atom_codes('Vicky',X).
X = [86, 105, 99, 107, 121]
yes

?- atom_codes('Vicky Pollard',X).
X = [86, 105, 99, 107, 121, 32, 80, 111, 108|...]
yes
\end{LPNcodedisplay}

It also works the other way around: \texttt{atom\_codes/2} can also be used
to generate atoms from strings. Suppose you want to duplicate an atom
\texttt{abc} into the atom {abcabc}. This is how you could do it:

\begin{LPNcodedisplay}
?- atom_codes(abc,X), append(X,X,L), atom_codes(N,L).

X = [97, 98, 99]
L = [97, 98, 99, 97, 98, 99]
N = abcabc
\end{LPNcodedisplay}

One last thing you need to know about the \texttt{atom\_codes/2} predicate is
that it is related to another other built-in predicate, namely
\texttt{number\_codes/2}\index{PROLOG number\_codes/2@\texttt{number\_codes/2}}.
This predicate behaves in a similar way, but, as the names suggest, only works for numbers.



\section{Operators}\label{SEC.L9.OPERATORS}


As we have seen, in certain cases (for example, when performing
arithmetic) Prolog lets us use operator notations that are more
user-friendly than its own internal representations.  Indeed, as we
shall now see, Prolog even has a mechanism for letting us define our
own operators.  In this section we'll first take a closer look at the
properties of operators, and then learn how to define our own.


\subsection*{Properties of operators}\label{SUBSEC.L9.OPERATORS.PROP}


Let's start with an example from arithmetic.  Internally, Prolog uses
the expression "is(11,+(2,*(3,3)))", but we are free to write the
functors "*" and "+" between their arguments, to form the more
user-friendly
expression "11 is 2 + 3 * 3".  Functors that can be written
between their arguments are called \LPNterm{infix operators}. Other
examples of infix operators in Prolog are ":-", "-->", ";", "','",
"=", "=..", "==" and so on.  In addition to infix operators there are
also \LPNterm{prefix operators} (which are written before their
arguments) and \LPNterm{postfix operators} (which are written after).
For example, "?-" is a prefix operator, and so is the one-place "-"
which is used to represent negative numbers (as in "1 is 3 + -2"). An
example of a postfix operator is the "++" notation used in the C
programming language to increment the value of a variable.

When we learned about arithmetic in Prolog, we saw that Prolog knows
about the conventions for disambiguating arithmetic expressions. So
when we write "2 + 3 * 3", Prolog knows that we mean "2 + (3 * 3)" and
not "(2 + 3) * 3". But how does Prolog know this? Because every
operator has a certain \LPNterm{precedence}. The precedence of "+" is
greater than the precedence of "*", and that's why "+" is taken to be
the main functor of the expression "2 + 3 * 3". (Note that Prolog's
internal representation "+(2,*(3,3))" is not ambiguous.)  Similarly,
the precedence of "is" is higher than the precedence of "+", so
"11 is 2 + 3 * 3" is interpreted as "is(11,+(2,*(3,3)))" and not as the
(nonsensical) expression "+(is(11,2),*(3,3))".  In Prolog, precedence
is expressed by a number between 0 and 1200; the higher the number,
the greater the precedence.  To give some examples, the precedence of
"=" is 700, the precedence of "+" is 500, and the precedence of "*" is
400.


What happens when there are several operators with the same
precedence in one expression? We said above that Prolog finds the
query "2 =:= 3 == =:=(2,3)" confusing. It doesn't know how to
bracket the expression: Is it "=:=(2,==(3,=:=(2,3)))" or is it
"==(=:=(2,3),=:=(2,3))"? The reason Prolog is not able to decide on
the correct bracketing is because "==" and "=:=" have the same
precedence. In such cases, explicit bracketings must be supplied by
the programmer.

What about the following query though?
\begin{LPNcodedisplay}
?- X is 2 + 3 + 4.
\end{LPNcodedisplay}
Does Prolog find this  confusing? Not at all: it deals with it happily
and correctly answers "X = 9".  But which bracketing did Prolog
choose: "is(X,+(2,+(3,4)))" or "is(X,+(+(2,3),4))"?
As the following queries show, it chose the second:

\begin{LPNcodedisplay}
?- 2 + 3 + 4 = +(2,+(3,4)).
no
?- 2 + 3 + 4 = +(+(2,3),4).
yes
\end{LPNcodedisplay}


Here Prolog has used information about the \LPNterm{associativity} of
"+"  to disambiguate:  "+" is \LPNterm{left
associative}, which means that the expression to the right of "+" must
have a lower precedence than "+" itself, whereas the expression on the
left may have the same precedence as "+". The precedence of an
expression is simply the precedence of its main operator,  or 0 if it
is enclosed in brackets.  The main operator of "3 + 4" is "+", so that
interpreting "2 + 3 + 4" as "+(2,+(3,4))" would mean that the
expression to the right of the first "+" has the same precedence as
"+" itself, which is illegal. It has to be lower.

The operators "==", "=:=", and "is" are defined to be
\LPNterm{non-associative}, which means that both of their arguments must have
a lower precedence. Therefore "2 =:= 3 == =:=(2,3)" is an illegal
expression,
since no matter how you bracket it you'll get a conflict:
"2 =:= 3" has the same precedence as "==", and
"3 == =:=(2,3)" has the same precedence as "=:=".

The type of an operator (infix, prefix, or postfix), its precedence,
and its associativity are the three things that Prolog needs to know
to be able to translate user-friendly (but potentially ambiguous)
operator notations into Prolog's internal representation.



\subsection*{Defining operators}\label{SUBSEC.L9.OPERATORS.DEF}



In addition to providing a user-friendly operator notation for certain
functors, Prolog also lets you define your own operators. So you
could, for example, define a postfix operator "is_dead"; then
Prolog would allow you to write "zed is_dead" as a fact in your
database instead of "is_dead(zed)".

Operator definitions in Prolog look like this:  \index{PROLOG op/3@\texttt{op/3}}
\begin{LPNcodedisplay}
:- op(Precedence,Type,Name).
\end{LPNcodedisplay}
As we mentioned above, precedence is a number between 0 and 1200, and
the higher the number, the greater the  precedence.
Type is an
atom specifying the type and associativity of the operator. In the
case of "+" this atom is "yfx", which says that "+" is an infix
operator; the "f" represents the operator,  and the "x" and "y" the
arguments.  Furthermore, "x" stands for an argument which has a
precedence which is lower than the precedence of "+" and "y" stands
for an argument which has a precedence which lower or equal to the
precedence of "+".  There are the following possibilities for
type:
\begin{center}\begin{tabular}{ll}
infix&"xfx", "xfy", "yfx"\\
prefix&"fx", "fy"\\
suffix&"xf", "yf"
\end{tabular}\end{center}

So  your operator definition for "is_dead" might be as follows:
\begin{LPNcodedisplay}
:- op(500, xf, is_dead).
\end{LPNcodedisplay}


Here are the definitions for some of the built-in operators. You can see
that operators with the same properties can be specified in one statement
by giving a list of their names (instead of a single name) as the third
argument of "op".

\begin{center}\begin{tabular}{l}
":- op( 1200, xfx, [ :-, --> ])."\\
":- op( 1200,  fx, [ :-, ?- ])."\\
":- op( 1100, xfy, [ ; ])."\\
":- op( 1000, xfy, [ ',' ])."\\
":- op(  700, xfx, [ =, is, =.., ==, \==, "\\
"                    =:=, =\=, <, >, =<, >= ])."\\
":- op(  500, yfx, [ +, -])."\\
":- op(  500,  fx, [ +, - ])."\\
":- op(  300, xfx, [ mod ])."\\
":- op(  200, xfy, [ ^ ])."
\end{tabular}\end{center}

One final point should made explicit. Operator definitions don't
specify the \LPNemph{meanings} of operators, they only describe how
they can be used syntactically. That is, an operator definition
doesn't say anything about when a query involving this operator will
evaluate to true, it merely extends the \LPNemph{syntax} of
Prolog. So if the operator "is_dead" is defined as above, and you pose
the query "zed is_dead", Prolog won't complain about illegal syntax
(as it would without this definition) but  will try to prove the
goal "is_dead(zed)", which is Prolog's internal representation of
"zed is_dead". And this is all operator definitions do --- they just tell
Prolog how to translate a user-friendly notation into real Prolog
notation. So, what would be Prolog's answer to the query
"zed is_dead"? It would be "no", because Prolog would try to prove
"is_dead(zed)", but would not find any matching clause in the
database.  But suppose we extended the database as follows:
\begin{LPNcodedisplay}
:- op(500, xf, is_dead).

kill(marsellus,zed).
is_dead(X) :- kill(_,X).
\end{LPNcodedisplay}
Now Prolog would answer "yes" to the query.


\section{Exercises}\label{SEC.L9.EXERCISES}

\begin{LPNexercise}{L9.EX1}Which of the following queries succeed, and which fail?
\begin{LPNcodedisplay}
?- 12 is 2*6.

?- 14 =\= 2*6.

?- 14 = 2*7.

?- 14 == 2*7.

?- 14 \== 2*7.

?- 14 =:= 2*7.

?- [1,2,3|[d,e]] == [1,2,3,d,e].

?- 2+3 == 3+2.

?- 2+3 =:= 3+2.

?- 7-2 =\= 9-2.

?- p == 'p'.

?- p =\= 'p'.

?- vincent == VAR.

?- vincent=VAR, VAR==vincent.

\end{LPNcodedisplay}
\end{LPNexercise}

\begin{LPNexercise}{L9.EX2}How does Prolog respond to the following queries?
\begin{LPNcodedisplay}
?- .(a,.(b,.(c,[]))) = [a,b,c].

?- .(a,.(b,.(c,[]))) = [a,b|[c]].

?- .(.(a,[]),.(.(b,[]),.(.(c,[]),[]))) = X.

?- .(a,.(b,.(.(c,[]),[]))) = [a,b|[c]].
\end{LPNcodedisplay}
\end{LPNexercise}

\begin{LPNexercise}{L9.EX3}Write a two-place predicate "termtype(Term,Type)" that
takes a term and gives back the type(s) of that term (atom, number,
constant, variable, and so on). The types should be given back in the order
of their generality. The predicate should  behave in the
following way.
\begin{LPNcodedisplay}
?- termtype(Vincent,variable).
yes
?- termtype(mia,X).
X = atom ;
X = constant ;
X = simple_term ;
X = term ;
no
?- termtype(dead(zed),X).
X = complex_term ;
X = term ;
no
\end{LPNcodedisplay}
\end{LPNexercise}


\begin{LPNexercise}{L9.EX4}Write a
Prolog program that defines the predicate "groundterm(Term)" which
tests whether or not "Term" is a ground term. Ground terms are terms
that don't contain variables. Here are examples of how the predicate
should behave:
\begin{LPNcodedisplay}
?- groundterm(X).
no
?- groundterm(french(bic_mac,le_bic_mac)).
yes
?- groundterm(french(whopper,X)).
no
\end{LPNcodedisplay}
\end{LPNexercise}

\begin{LPNexercise}{L9.EX5}Assume that we have the following operator definitions.
\begin{LPNcodedisplay}
:- op(300, xfx, [are, is_a]).
:- op(300, fx, likes).
:- op(200, xfy, and).
:- op(100, fy, famous).
\end{LPNcodedisplay}
Which of the following are well-formed terms?
What are the main operators? Give the bracketings.

\begin{LPNcodedisplay}
X is_a witch
harry and ron and hermione are friends
harry is_a wizard and likes quidditch
dumbledore is_a famous wizard
\end{LPNcodedisplay}
\end{LPNexercise}



\section{Practical Session}\label{SEC.L9.PRAXIS}



To start this session, we'll introduce some built-in predicates for
printing terms onto the screen. You should try out the following
examples as we introduce them.  The first predicate we want to look at
is "display/1".\index{PROLOG display/1@\texttt{display/1}} Here are
some simple examples:
\begin{LPNcodedisplay}
?- display(loves(vincent,mia)).
loves(vincent, mia)

yes
?- display('jules eats a big kahuna burger').
jules eats a big kahuna burger

yes
\end{LPNcodedisplay}

But the really important point about "display/1", as the following
examples demonstrate, is that it prints Prolog's \LPNemph{internal
representation} of terms to the screen:

\begin{LPNcodedisplay}
?- display(2+3+4).
+(+(2, 3), 4)

yes
\end{LPNcodedisplay}


This property of "display/1" makes it a very useful tool for learning
how operators work in Prolog. So, before going on, try the following
queries. Make sure you understand why Prolog answers the way it does.

\begin{LPNcodedisplay}
?- display([a,b,c]).
?- display(3 is 4 + 5 / 3).
?- display(3 is (4 + 5) / 3).
?- display((a:-b,c,d)).
?- display(a:-b,c,d).
\end{LPNcodedisplay}


So "display/1" is useful when we want to look at the internal
representation of terms in operator notation. But often we would
prefer to see the user-friendly notation instead. For example, when
reading lists it is usually more pleasant to see "[a,b,c]" rather
than ".(a.(b.(c,[])))". The built-in predicate "write/1" lets us
view terms like
this.\index{PROLOG write/1@\texttt{write/1}} This predicate takes a
term and prints it to the screen in the user-friendly notation.

\begin{LPNcodedisplay}
?- write(2+3+4).
2+3+4
yes

?- write(+(2,3)).
2+3
yes

?- write([a,b,c]).
[a, b, c]
yes

?- write(.(a,.(b,[]))).
[a, b]
yes
\end{LPNcodedisplay}


And here is what happens when the term  to be written contains
variables:

\begin{LPNcodedisplay}
?- write(X).
_G204
X = _G204
yes

?- X = a, write(X).
a
X = a
yes
\end{LPNcodedisplay}


The following example shows what happens when you give two  "write/1"
commands one after the other:
\begin{LPNcodedisplay}
?- write(a),write(b).
ab

yes
\end{LPNcodedisplay}
That is, Prolog just executes one after the other without putting any
space in between the output of the two commands. Of
course, you can get Prolog to print space by telling it to write the
term "' '":
\begin{LPNcodedisplay}
?- write(a),write(' '),write(b).
a b

yes
\end{LPNcodedisplay}
And if you want more than one space, for example five blanks, you
can tell Prolog to write "'     '".
\begin{LPNcodedisplay}
?- write(a),write('     '),write(b).
a     b

yes
\end{LPNcodedisplay}

Another way of printing spaces is by using the predicate
"tab/1".\index{PROLOG tab/1@\texttt{tab/1}} This takes a number as argument and then
prints  that number of spaces:
\begin{LPNcodedisplay}
?- write(a),tab(5),write(b).
a     b

yes
\end{LPNcodedisplay}


Another predicate useful for formatting is "nl".\index{PROLOG nl/0@\texttt{nl/0}}
This  tells Prolog to make a line-break and to go on printing on
the next line.
\begin{LPNcodedisplay}
?- write(a),nl,write(b).
a
b
yes
\end{LPNcodedisplay}


Time to apply what you have just learned.
In the last chapter we saw how extra arguments in DCGs could be used
to build  parse trees. For example, to the query
\begin{LPNcodedisplay}
s(T,[a,man,shoots,a,woman],[])
\end{LPNcodedisplay}
Prolog would answer
\begin{LPNcodedisplay}
s(np(det(a),n(man)),vp(v(shoots),np(det(a),n(woman)))).
\end{LPNcodedisplay}
This term is a representation of the parse tree, but it is not a very readable
representation. It would be nicer if Prolog printed
something like the following (this style
of printing is usually called \LPNterm{pretty printing}):
\begin{LPNcodedisplay}
s(
  np(
     det(a)
     n(man))
  vp(
    v(shoots)
    np(
      det(a)
      n(woman))))
\end{LPNcodedisplay}
Write a predicate "pptree/1" that takes a complex term
representing a tree
as its argument and prints
the tree in a more readable form.

Time to  practice writing operator definitions.
In the practical session of Chapter~\ref{CHAPTER7}, you were asked to
write a DCG generating propositional logic formulas. The input you had
to use was a bit awkward though. The formula $\neg (p \rightarrow q)$
had to be represented as "[not, '(', p, implies, q, ')']". Now that
you know about operators, you can do things rather more neatly. Write
operator definitions for  "not", "and", "or", and
"implies", so that Prolog accepts (and correctly brackets)
propositional logic formulas. Use "display/1" to check
your code.
It should yield the  following kinds of response:
\begin{LPNcodedisplay}
?- display(not(p implies q)).
not(implies(p,q)).

yes

?- display(not p implies q).
implies(not(p),q)

yes
\end{LPNcodedisplay}
